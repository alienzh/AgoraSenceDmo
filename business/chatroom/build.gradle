if (isChatroomModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
//plugins {
//    id 'com.android.library'
//    id 'org.jetbrains.kotlin.android'
//}

def dep = rootProject.ext.dependencies
def androidVersion = rootProject.ext.android

android {
    compileSdk androidVersion["compileSdkVersion"]

    defaultConfig {
        minSdk androidVersion["minSdkVersion"]
        targetSdk androidVersion["targetSdkVersion"]
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
//        if (isChatroomModule.toBoolean()) {
//            // 独立调试时添加 applicationId ，集成调试时移除
//            applicationId "io.agora.chatroom"
//        }
    }
    sourceSets {
        main {
            // 独立调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isChatroomModule.toBoolean()) {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {

    api dep["core-ktx"]
    api dep["appcompat"]
    api dep["constraintlayout"]
    api dep["recyclerview"]
    api dep["material"]

    testImplementation dep["junit"]
    androidTestImplementation dep["junit-android"]
    androidTestImplementation dep["espresso-core"]

    implementation dep["arouter-api"]
    kapt dep["arouter-compiler"]

    if (isChatroomModule.toBoolean()) {
        implementation project(":basicsdepend")
    }else {
        compileOnly project(':common:buddy')
        compileOnly project(':common:baseui')
        compileOnly project(':common:config')
        compileOnly project(':component:secnceui')
    }
}